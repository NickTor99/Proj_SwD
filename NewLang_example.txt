|* declaration of a global variable *|
var menu << "(1) somma di due numeri\n" &
            "(2) moltiplicazione di due numeri\n" &
            "(3) divisione intera tra due numeri\n" &
            "(4) elevamento a potenza\n" &
            "(5) succesione di fibonacci\n" &
            "(6) calcolo fattoriale di n\n" &
            "(7) somma di n numeri";

|| the main function
start: def inizio(integer boh | out string ciao): void{
    string size << "piccolo";
    var ans << "si";
    integer op;
    var num1 << 0.0, num2 << 0.0, num << 0;
    while ans = "si" loop{
        ("Scegli che operazione fare inserendo il numero corrispondente:\n", menu) -->!;
        op <--;
        if op = 1 or op = 2 or op = 3 or op = 4 then{
            num1 <-- "inserisci in primo numero ";
            num2 <-- "inserisci il secondo numero ";
            if op = 3 then{
                ("il risultato e': ",divIntera(num1,num2,size),", un numero " & size & "!") -->!;
            }
            else{
                ("il risultato e': ",operazione(op,num1,num2)) -->!;
            }
        }
        else{
            if op = 5 or op = 6 then{
                num <-- "inserisci un numero intero positivo ";
                while num < 0 loop{
                    num <-- "il numero deve essere positivo! Prova ad inserirlo di nuovo ";
                }
                if op = 5 then{
                    ("il risultato e': ",fibonacci(num)) -->!;
                }
                if op = 6 then{
                    ("il risultato e': ",fattoriale(num)) -->!;
                }
            }
            else{
                if op = 7 then{
                    integer n;
                    float risultato;
                    n <-- "quanti numeri vuoi inserire? ";
                    risultato << somma_di_n(n);
                    ("il risultato e': ",risultato) -->!;
                }
                else{
                    ("hai inserito un numero sbagliato!") -->!;
                }
            }
        }
        ans <-- "vuoi continuare? (si/no)";
    }
    ("Arrivederci :)") -->;
}

def operazione(integer op | float a | float b): float{
    if op = 1 then{
        return a + b;
    }
    if op = 2 then{
        var result << 0.0;
        while a >= 1 loop{
            a << a-1;
            result << result + b;
        }
        return result + (a*b);
    }
    if op = 4 then{
        return a^b;
    }
}

def fibonacci(integer a): integer{
    integer risultato << 0;
    if a = 0 then{
        return 0;
    }
    if a = 1 then{
        return 1;
    }
    return fibonacci(a-1) + fibonacci(a-2);
}

integer setSize << 10 + costante;
var costante << 5;

def divIntera(float a | float b | out string size): integer{
    integer result << a/b;
    if result >= setSize then{
        size << "grande";
    }
    return result;
}

def somma_di_n(integer n): float{
    float somma << 0.0;
    while n != 0 loop{
        float num;
        num <-- "inserisci un numero: ";
        somma << somma + num;
        n << n-1;
    }
    return somma;
}

def fattoriale(integer n): integer{
    integer result;
    if n = 0 then{
        return 1;
    }
    else{
        result << n * fattoriale(n-1);
        return result;
    }
}

|| other examples of types of variables
boolean var1 << not(true and false) or true;
char var2, var3, var4 << 'c';